% This code takes ast facts and performes required stratification for negation and aggregation.
% It is assumed that input ast is valid (no negation or aggregation in dependency cycles).
%
% This code has explicitly specified stratification $stratum and $stratum_dependency,
% which makes it possible to execute it by semi-positive Datalog.

'$meta stratum'(a, ast_clause)@0;
'$meta stratum'(a, ast_body_expr)@0;
'$meta stratum'(a, ast_body_atom)@0;
'$meta stratum'(a, ast_atom)@0;
'$meta stratum'(a, ast_clause_var_arg)@0;
'$meta stratum'(a, ast_body_var_arg)@0;
'$meta stratum'(a, symbol)@0;
'$meta stratum'(a, initial_fact)@0;
'$meta stratum'(a, negated_dependency)@0;
'$meta stratum'(a, aggregated_dependency)@0;
'$meta stratum'(a, body_atom_uses_var)@0;
'$meta stratum'(a, aggregated_clause_var)@0;

'$meta stratum'(b, path)@0;
'$meta stratum'(b, cycle)@0;
'$meta stratum'(b, vertex)@0;
'$meta stratum'(b, dependency)@0;

'$meta stratum'(c, leaf)@0;
'$meta stratum'(c, stratum_option)@0;

'$meta stratum'(d, stratum_number)@0;
'$meta stratum'(d, stratum)@0;
'$meta stratum'(d, stratum_dependency)@0;

% $meta stratum_dependency forms a DAG, no cycles allowed.
% Each stratum contains set of rules, which are vertices in rule dependency graph.
% Basically we specify DAG of subgraphs.
%
% We could specify exact rule ordering for computation,
% use numbers for stratum identification instead of symbols,
% but thay would introduce more order than necessary.
%
% Better to keep things unordered as long as it is possible,
% to keep opportunities for optimization open.
'$meta stratum_dependency'(b, a)@0;
'$meta stratum_dependency'(c, b)@0;
'$meta stratum_dependency'(d, c)@0;
'$meta stratum_dependency'(d, a)@0;



% Okay, so I need to write stratification code in Dedalus
% using negation and successor and then stratify it by hand.


% Algorithm:
% 1) find all leaf dependencies
% 2) take first stratum number, assign it to leaf vertices
% 3) if all children are positive and have stratum A, then vertex also has stratum A
% 4) if vertex has negative child, where all children belong to A1, A2, ...,
%    then vertex has stratum max(A1, A2, ...)+1
% 5) if vertex belongs to cycle, when some of its children have stratum A1, A2, then its stratum is max(A1, A2, ...)+1



initial_fact(Name) :-
  ast_atom(Name, _AtomId, _Timestamp);

negated_dependency(Name1, Name2) :-
  ast_clause(Name1, ClauseId, Suffix),
  Suffix = none,
  ast_body_expr(ClauseId, ExprId),
  ast_body_atom(ExprId, Name2, Negated),
  Negated = true;

aggregated_dependency(Name1, Name2) :-
  ast_clause(Name1, ClauseId, Suffix),
  Suffix = none,
  % ast_body_expr(ClauseId, ExprId),
  % ast_body_atom(ExprId, Name2, Negated),
  aggregated_clause_var(ClauseId, VarName),
  body_atom_uses_var(ClauseId, Name2, VarName);

aggregated_clause_var(ClauseId, VarName) :-
  ast_clause_var_arg(ClauseId, _ArgN, VarName, AggFunc, _LocPrefix),
  AggFunc =/= none;

body_atom_uses_var(ClauseId, Name, VarName) :-
  ast_body_expr(ClauseId, ExprId),
  ast_body_atom(ExprId, Name, Negated),
  ast_body_var_arg(ExprId, _ArgN, VarName, _LocPrefix);

dependency(Name1, Name2, NeedStratumChange) :-
  negated_dependency(Name1, Name2),
  NeedStratumChange = true;
dependency(Name1, Name2, NeedStratumChange) :-
  aggregated_dependency(Name1, Name2),
  NeedStratumChange = true;
dependency(Name1, Name2, NeedStratumChange) :-
  ast_clause(Name1, ClauseId, Suffix),
  Suffix = none,
  ast_body_expr(ClauseId, ExprId),
  ast_body_atom(ExprId, Name2, _Negated),

  notin negated_dependency(Name1, Name2),
  notin aggregated_dependency(Name1, Name2),
  NeedStratumChange = false;



path(Name1, Name2) :-
  dependency(Name1, Name2, _NeedStratumChange);
path(Name1, Name2) :-
  dependency(Name1, Name0, _NeedStratumChange),
  path(Name0, Name2);

cycle(Name) :-
  path(Name, Name);

vertex(A) :-
  dependency(A, _B, _NeedStratumChange);
vertex(B) :-
  dependency(_A, B, _NeedStratumChange);

leaf(A) :-
  vertex(A),
  vertex(B),
  notin dependency(A, B, true),
  notin dependency(A, B, false);

% stratum_option computes one of the possible stratum numbers
% that given vertex may have. In the end only max stratum would be selected.

stratum_option(V, Stratum) :-
  leaf(V),
  Stratum = 1;
stratum_option(V, Stratum) :-
  % this stratum option likely would be overshadowed
  % by other rules. We need this for the case
  % if we have network of self-referencing cycles
  % that have no edges with leaf vertices at all.
  % In that case at least one initial_fact rule
  % inside this subgraph would spread stratum across.
  initial_fact(V),
  Stratum = 1;
stratum_option(Parent, Stratum) :-
  % if we have negative child with stratum,
  % then parent stratum must be at least +1
  dependency(Parent, Child, NeedStratumChange),
  NeedStratumChange = true,
  stratum_option(Child, Stratum1),
  % TODO: turns out it is not correct to use successor this way
  % it cannot produce new values, only confirm that existing
  % values follow each other.
  %
  % Need to rewrite this code to produce
  % new stratum on each @next tick
  successor(Stratum1, Stratum);
stratum_option(Parent, Stratum) :-
  % if have positive child, then just propagate its choosen stratum.
  % if parent has negative children, then this propagated value
  % will be disregared in favor of larger one.
  dependency(Parent, Child, NeedStratumChange),
  NeedStratumChange = false,
  stratum_option(Child, Stratum);



stratum_number(Name, max<Stratum>) :-
  stratum_option(Name, Stratum);

stratum(Name, StratumSym) :-
  stratum_number(Name, Stratum),
  symbol(Stratum, StratumSym);

stratum_dependency(ParentStratum, ChildStratum) :-
  dependency(Parent, Child, _NeedStratumChange),
  stratum(Parent, ParentStratum),
  stratum(Child, ChildStratum);

% It would be better to use symbols for stratums
% to explicitly show that they are not ordered,
% only stratum_dependency define ordering
symbol(1, a)@1; symbol(2, b)@1; symbol(3, c)@1; symbol(4, d)@1; symbol(5, e)@1;
symbol(6, f)@1; symbol(7, g)@1; symbol(8, h)@1; symbol(9, i)@1; symbol(10, j)@1;
symbol(11, k)@1; symbol(12, l)@1; symbol(13, m)@1; symbol(14, n)@1; symbol(15, o)@1;
symbol(16, p)@1; symbol(17, q)@1; symbol(18, r)@1; symbol(19, s)@1; symbol(20, t)@1;
symbol(21, u)@1; symbol(22, v)@1; symbol(23, w)@1; symbol(24, x)@1; symbol(25, y)@1;
symbol(26, z)@1;
